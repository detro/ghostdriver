/*
This file is part of the GhostDriver by Ivan De Marino <http://ivandemarino.me>.

Copyright (c) 2012-2014, Ivan De Marino <http://ivandemarino.me>
All rights reserved.

Redistribution and use in source and binary forms, with or without modification,
are permitted provided that the following conditions are met:

    * Redistributions of source code must retain the above copyright notice,
      this list of conditions and the following disclaimer.
    * Redistributions in binary form must reproduce the above copyright notice,
      this list of conditions and the following disclaimer in the documentation
      and/or other materials provided with the distribution.

THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS" AND
ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED
WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE
DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE LIABLE FOR
ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES
(INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES;
LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON
ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
(INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS
SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
*/

apply plugin: "java"
apply plugin: "idea"
apply plugin: "eclipse"
apply plugin: "maven"
apply plugin: "signing"

task wrapper(type: Wrapper) {
    gradleVersion   = "2.5"
    jarFile         = "gradle/gradle-wrapper.jar"
}

repositories {
    mavenCentral()
}

compileJava {
    options.compilerArgs.add("-Xlint:unchecked")
    options.compilerArgs.add("-Xlint:deprecation")
}

project.archivesBaseName    = project.name
project.version             = "1.2.0"

ext.projectGroupId          = "com.github.detro"
ext.projectArtifactId       = "phantomjsdriver"
ext.projectLongName         = "PhantomJS Driver (GhostDriver)"
ext.projectArtefactDir      = "jars/"
ext.projectMainClass        = "org.openqa.selenium.phantomjs.PhantomJSDriver"
ext.projectCreator          = "Ivan De Marino (https://github.com/detro)"

ext.seleniumVersion         = "2.41.0"

dependencies {
    ["selenium-java", "selenium-remote-driver"].each {
        compile "org.seleniumhq.selenium:$it:$seleniumVersion"
    }
}

// Forces Java 6 compile source/target
compileJava {
    sourceCompatibility = 1.6
    targetCompatibility = 1.6
}

tasks.withType(JavaExec) {
    classpath = configurations.compile + sourceSets.test.output
    args project.hasProperty("args") ? project.args.split("\\s") : []
}

// Binary Jar
jar {
    destinationDir      = file(projectArtefactDir)
    version             = project.version
    manifest {
        attributes(
            "Implementation-Title"      : projectLongName,
            "Implementation-Version"    : project.version,
            "Gradle-Version"            : project.gradle.gradleVersion,
            "Created-By"                : projectCreator
        )
    }
}

// Source Jar
task sourceJar(type: Jar, dependsOn: classes) {
    classifier = "sources"
    from sourceSets.main.allSource

    destinationDir      = file(projectArtefactDir)
    version             = project.version
    manifest {
        attributes(
            "Implementation-Title"      : projectLongName,
            "Implementation-Version"    : project.version,
            "Gradle-Version"            : project.gradle.gradleVersion,
            "Created-By"                : projectCreator
        )
    }
}

// JavaDoc Jar
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = "javadoc"
    from javadoc.destinationDir

    destinationDir      = file(projectArtefactDir)
    version             = project.version
    manifest {
        attributes(
            "Implementation-Title"      : projectLongName,
            "Implementation-Version"    : project.version,
            "Gradle-Version"            : project.gradle.gradleVersion,
            "Created-By"                : projectCreator
        )
    }
}

// Make all Jars
task jars {
    dependsOn jar, sourceJar, javadocJar
}

// Store JavaDoc
task storeJavadoc(type: Sync, dependsOn: javadoc) {
    from project.docsDir
    into "docs"
}

// Artifact-making Tasks
artifacts {
    archives jar
    archives javadocJar
    archives sourceJar
}

// Sign artifacts
signing {
    sign configurations.archives
}

// Upload archives to OSS Sonatype
//
// NOTE: requires you to first setup
// 1) ~/.gnupg/* with a PGP key to sign the artifacts
// 2) ~/.gradle/gradle.properties defining
//
//    signing.keyId=<YOUR PGP KEY ID>
//    signing.password=<PASSWORD TO PGP SECRET RING>
//    signing.secretKeyRingFile=<FULLPATH TO PGP SECRET RING>
//    ossrhUsername=<OSS Sonatype Username>
//    ossrhPassword=<OSS Sonatype Password>
//
uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment {
                MavenDeployment deployment -> signing.signPom(deployment)
            }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name            "PhantomJSDriver"
                description     """
                                PhantomJSDriver is a Java binding for the PhantomJS WebDriver, GhostDriver.
                                The binding is developed within the GhostDriver project, and distributed through public Maven repository
                                and Selenium official .zip package.
                                """
                groupId         projectGroupId
                artifactId      projectArtifactId
                version         project.version
                packaging       "jar"
                url             "https://github.com/detro/ghostdriver"
                inceptionYear   "2012"

                scm {
                    url                     "https://github.com/detro/ghostdriver"
                    connection              "scm:git:git@github.com:detro/ghostdriver.git"
                    developerConnection     "scm:git:git@github.com:detro/ghostdriver.git"
                }

                licenses {
                    license {
                        name            "The BSD 2-Clause License"
                        url             "http://opensource.org/licenses/BSD-2-Clause"
                        distribution    "repo"
                    }
                }

                developers {
                    developer {
                        id          "detro"
                        name        "Ivan De Marino"
                        email       "detronizator@gmail.com"
                        url         "http://ivandemarino.me"
                        roles {
                            role    "Developer"
                        }
                        timezone    "GMT"
                    }
                }
            }
        }
    }
}
